---
title: "Enterprise Ready (?) ãª Minecraft ã‚µãƒ¼ãƒãƒ¼ã‚’æ§‹ç¯‰ã™ã‚‹"
emoji: ""
type: "tech" # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: ["Minecraft", "GameServer", "Docker", "Azure", "Azure Container Apps", "Azure Firewall", "Azure Files"]
published: true
---

> å¤‰æ›´å±¥æ­´
> - 2025-12-20: Bicep ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆå…¨ä½“ã¨ã€ç›£è¦–ã«ã¤ã„ã¦è¿½è¨˜

ã“ã‚“ã«ã¡ã‚ã€‚
[Microsoft Advent Calendar 2025](https://qiita.com/advent-calendar/2025/microsoft-azure-tech) ã® 12 æœˆ 19 æ—¥ã®è¨˜äº‹ã§ã™ğŸ„

![alt text](/images/5bed15e0ba0036/mc1.png)

2å¹´å‰ã« Azure ä¸Šã¸ Minecraft ã‚µãƒ¼ãƒãƒ¼ã‚’æ§‹ç¯‰ã™ã‚‹è©±ã‚’ã—ã¦ã¾ã—ãŸ [^1]ã€‚
å‰å›ã¯ VM ã«å±•é–‹ã—ãŸã ã‘ã§ã‚»ã‚­ãƒ¥ãƒªãƒ†ã‚£é¢ã€ç®¡ç†æ€§ãªã©è€ƒæ…®ãŒè–„ã‹ã£ãŸã¨æ€ã„ã¾ã™ã€‚
ä»Šå›ã¯ã€ã‚ˆã‚Šã‚»ã‚­ãƒ¥ã‚¢ã§ã€Azure ã®ãƒ™ã‚¹ãƒˆãƒ—ãƒ©ã‚¯ãƒ†ã‚£ã‚¹ã«ã°ã£ã¡ã‚Šæ²¿ã£ãŸè¨­å®šãŒã•ã‚Œã¦ã„ã‚‹ Azure Verified Module (AVM) [^2] ã¨å‘¼ã°ã‚Œã‚‹ Infrastructure-as-Code (IaC) ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ã£ã¦æ§‹ç¯‰ã—ã¾ã™ã€‚

## Azure Verified Module ã¨ã¯

ã¯ã˜ã‚ã«ã€Azure Verified Module (AVM) ã¨ã¯ã€Microsoft ãŒä¸»å°ã™ã‚‹ Infrastructure-as-Code (IaC) ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®æ¨™æº–åŒ–ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆã§ã™ã€‚Bicep ã¨ Terraform ã®ä¸¡æ–¹ã«å¯¾å¿œã—ã¦ãŠã‚Šã€Azure ãƒªã‚½ãƒ¼ã‚¹ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹ãŸã‚ã®å†åˆ©ç”¨å¯èƒ½ãªãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ãŒå…¬å¼ãƒ¬ã‚¸ã‚¹ãƒˆãƒªã§å…¬é–‹ã•ã‚Œã¦ã„ã¾ã™ã€‚

ã“ã®ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¯ Microsoft ã«ã‚ˆã£ã¦é–‹ç™ºãƒ»ä¿å®ˆã•ã‚Œã¦ãŠã‚Šã€Well-Architected Framework [^4] ã®æ¨å¥¨äº‹é …ã«æ²¿ã£ãŸè¨­å®šãŒãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§é©ç”¨ã•ã‚Œã¾ã™ã€‚ã“ã‚Œã«ã‚ˆã‚Šã€ã‚»ã‚­ãƒ¥ãƒªãƒ†ã‚£ã‚„ãƒ‘ãƒ•ã‚©ãƒ¼ãƒãƒ³ã‚¹ã®ãƒ™ã‚¹ãƒˆãƒ—ãƒ©ã‚¯ãƒ†ã‚£ã‚¹ã‚’æ„è­˜ã—ãªãã¦ã‚‚ã€ã‚ã‚‹ç¨‹åº¦ã®å“è³ªãŒæ‹…ä¿ã•ã‚ŒãŸçŠ¶æ…‹ã§ãƒªã‚½ãƒ¼ã‚¹ã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã§ãã¾ã™ã€‚

ã¾ãŸã€å„ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¯è¨­è¨ˆã€å®Ÿè£…ã€ãƒ†ã‚¹ãƒˆã€ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆç”Ÿæˆã€ãƒ¬ãƒ“ãƒ¥ãƒ¼ã¨ã„ã†ä¸€é€£ã®ãƒ—ãƒ­ã‚»ã‚¹[^5]ã‚’çµŒã¦å…¬é–‹ã•ã‚Œã¦ã„ã¾ã™ã€‚ãã®ç‚¹ã«ãŠã„ã¦ã‚‚ãƒ—ãƒ­ã‚¸ã‚§ã‚¯ãƒˆãªã©ã§æ¡ç”¨ã—ã‚„ã™ã„ã®ã§ã¯ãªã„ã§ã—ã‚‡ã†ã‹ã€‚

Bicep ã®å ´åˆã€AVM ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¯ Bicep Public Module Registry ã§å…¬é–‹ã•ã‚Œã¦ãŠã‚Šã€`br/public:avm/res/<provider>/<resource>:<version>` ã®å½¢å¼ã§å‚ç…§ã§ãã¾ã™ã€‚ãŸã¨ãˆã°ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã®ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã§ã‚ã‚Œã° `br/public:avm/res/network/virtual-network:0.5.2` ã¨ãªã‚Šã¾ã™ã€‚

ãªãŠã€Visual Studio Code ã® Bicep æ‹¡å¼µæ©Ÿèƒ½ã‚’ä½¿ç”¨ã—ã¦ã„ã‚‹å ´åˆã€ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¯å‚ç…§æ™‚ã«ãƒ­ãƒ¼ã‚«ãƒ«ã«ã‚­ãƒ£ãƒƒã‚·ãƒ¥ã•ã‚Œã¾ã™ [^6]ã€‚

![WSL2 ã®å ´åˆ](/images/5bed15e0ba0036/mc2.png)

Visual Studio Code ã§ä½¿ã£ã¦ã„ã‚‹å ´åˆã€ã‚«ãƒ¼ã‚½ãƒ«ã‚’å¯¾è±¡ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®ç®‡æ‰€ã«é‡ã­ã‚‹ã“ã¨ã§ã€ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã«ã‚¸ãƒ£ãƒ³ãƒ—ã™ã‚‹ã“ã¨ã‚‚ã§ãã¾ã™ã®ã§ã€å¿…é ˆ/ã‚ªãƒ—ã‚·ãƒ§ãƒ³ãƒ‘ãƒ©ãƒ¡ãƒ¼ã‚¿ã®èª¬æ˜ã‚„ã‚µãƒ³ãƒ—ãƒ«ã‚³ãƒ¼ãƒ‰ã‚’ç¢ºèªã§ãã¾ã™ã€‚

![alt text](/images/5bed15e0ba0036/mc3.png)

## ã‚¢ãƒ¼ã‚­ãƒ†ã‚¯ãƒãƒ£ã®å…¨ä½“åƒ

ãã‚Œã§ã¯ä»Šå›æ§‹ç¯‰ã™ã‚‹å…¨å®¹ã‚’ç´¹ä»‹ã—ã¾ã™ã€‚
ã‚¢ãƒ¼ã‚­ãƒ†ã‚¯ãƒãƒ£ã¨ã—ã¦ã¯ã€Azure Container Apps ä¸Šã§ Minecraft ã‚µãƒ¼ãƒãƒ¼ã‚’å‹•ä½œã•ã›ã€ã‚¤ãƒ³ã‚¿ãƒ¼ãƒãƒƒãƒˆã‹ã‚‰ã®ã‚¢ã‚¯ã‚»ã‚¹ã‚’ Azure Firewall ã§å—ã‘ä»˜ã‘ã‚‹æ§‹æˆã¨ãªã£ã¦ã„ã¾ã™ã€‚

![alt text](/images/5bed15e0ba0036/mc4.png)

ã“ã®æ§‹æˆã®ãƒã‚¤ãƒ³ãƒˆã¯ã€Container Apps ç’°å¢ƒã‚’å†…éƒ¨ (internal) ãƒ¢ãƒ¼ãƒ‰ã§ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã¦ã„ã‚‹ç‚¹ã«ã‚ã‚Šã¾ã™ã€‚Container Apps ã®å†…éƒ¨ç’°å¢ƒã¯ç›´æ¥ã‚¤ãƒ³ã‚¿ãƒ¼ãƒãƒƒãƒˆã«å…¬é–‹ã•ã‚Œãªã„ãŸã‚ã€å¤–éƒ¨ã‹ã‚‰ã®ã‚¢ã‚¯ã‚»ã‚¹ã¯ Azure Firewall ã® DNAT ãƒ«ãƒ¼ãƒ«ã‚’çµŒç”±ã—ã¦å—ã‘ä»˜ã‘ã¾ã™ã€‚ã“ã‚Œã«ã‚ˆã‚Šã€ãƒ•ã‚¡ã‚¤ã‚¢ã‚¦ã‚©ãƒ¼ãƒ«ã§ãƒˆãƒ©ãƒ•ã‚£ãƒƒã‚¯ã‚’ä¸€å…ƒçš„ã«åˆ¶å¾¡ã—ã¤ã¤ã€Minecraft ã‚µãƒ¼ãƒãƒ¼ã¸ã®æ¥ç¶šã‚’å®Ÿç¾ã—ã¦ã„ã¾ã™ã€‚

ã¾ãŸã€ãƒ¯ãƒ¼ãƒ«ãƒ‰ãƒ‡ãƒ¼ã‚¿ã‚’æ°¸ç¶šåŒ–ã™ã‚‹ãŸã‚ã« Azure Files ã‚’ä½¿ç”¨ã—ã€Private Endpoint çµŒç”±ã§ã‚»ã‚­ãƒ¥ã‚¢ã«ã‚¢ã‚¯ã‚»ã‚¹ã™ã‚‹æ§‹æˆã¨ã—ã¦ã„ã¾ã™ã€‚

## ä½¿ç”¨ã™ã‚‹ AVM ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«

ã“ã®æ§‹æˆã§ã¯ä»¥ä¸‹ã® AVM ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ç”¨ã—ã¾ã™ã€‚

| ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ« | ãƒãƒ¼ã‚¸ãƒ§ãƒ³ | ç”¨é€” |
|---|---|---|
| avm/res/network/virtual-network | 0.7.2 | ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã¨ã‚µãƒ–ãƒãƒƒãƒˆã®ä½œæˆ |
| avm/res/network/private-dns-zone | 0.8.0 | Storage Account ç”¨ã®ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆ DNS ã‚¾ãƒ¼ãƒ³ |
| avm/res/network/public-ip-address | 0.10.0 | Azure Firewall ç”¨ã®ãƒ‘ãƒ–ãƒªãƒƒã‚¯ IP ã‚¢ãƒ‰ãƒ¬ã‚¹ |
| avm/res/network/azure-firewall | 0.9.2 | Azure Firewall |
| avm/res/network/firewall-policy | 0.3.4 | ãƒ•ã‚¡ã‚¤ã‚¢ã‚¦ã‚©ãƒ¼ãƒ«ãƒãƒªã‚·ãƒ¼ã¨ãƒ«ãƒ¼ãƒ« |
| avm/res/operational-insights/workspace | 0.9.1 | Log Analytics ãƒ¯ãƒ¼ã‚¯ã‚¹ãƒšãƒ¼ã‚¹ |
| avm/res/storage/storage-account | 0.30.0 | Storage Account (Azure Files) |
| avm/res/app/managed-environment | 0.8.1 | Container Apps ç’°å¢ƒ |
| avm/res/app/container-app | 0.12.0 | Minecraft ã‚³ãƒ³ãƒ†ãƒŠã‚¢ãƒ—ãƒª |

## ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã®æ§‹æˆ

ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã¯ 4 ã¤ã®ã‚µãƒ–ãƒãƒƒãƒˆã§æ§‹æˆã•ã‚Œã¾ã™ã€‚

```bicep
module vnet 'br/public:avm/res/network/virtual-network:0.7.2' = {
  name: '${time}-privateVnet'
  params: {
    name: vnetName
    addressPrefixes: vnetAddressPrefixes
    subnets: [
      {
        name: subnetAzureFirewallName
        addressPrefix: subnetAzureFirewallPrefix
      }
      {
        name: subnetStorageName
        addressPrefix: subnetStoragePrefix
        privateLinkServiceNetworkPolicies: 'Enabled'
        defaultOutboundAccess: false
      }
      {
        name: subnetWebName
        addressPrefix: subnetWebPrefix
        delegation: 'Microsoft.App/environments'
      }
      {
        name: subnetAzureFirewallManagementName
        addressPrefix: subnetAzureFirewallManagementPrefix
      }
    ]
  }
}
```

AzureFirewallSubnet ã¯ Azure Firewall ã®ãƒ‡ãƒ¼ã‚¿ãƒ—ãƒ¬ãƒ¼ãƒ³ç”¨ã®ã‚µãƒ–ãƒãƒƒãƒˆã§ã™ã€‚Azure Firewall ã¯ã“ã®åå‰ã®ã‚µãƒ–ãƒãƒƒãƒˆã‚’å¿…è¦ã¨ã™ã‚‹ãŸã‚ã€åå‰ã‚’å¤‰æ›´ã™ã‚‹ã“ã¨ã¯ã§ãã¾ã›ã‚“ã€‚

storage ã‚µãƒ–ãƒãƒƒãƒˆã¯ Storage Account ã® Private Endpoint ã‚’é…ç½®ã™ã‚‹ãŸã‚ã®ã‚µãƒ–ãƒãƒƒãƒˆã§ã™ã€‚

app ã‚µãƒ–ãƒãƒƒãƒˆã¯ Container Apps ç’°å¢ƒç”¨ã®ã‚µãƒ–ãƒãƒƒãƒˆã§ã‚ã‚Šã€`delegation: 'Microsoft.App/environments'` ã‚’æŒ‡å®šã—ã¦ Container Apps ã¸ã®å§”ä»»ã‚’è¨­å®šã—ã¦ã„ã¾ã™ã€‚Container Apps ã®ãƒ¯ãƒ¼ã‚¯ãƒ­ãƒ¼ãƒ‰ãƒ—ãƒ­ãƒ•ã‚¡ã‚¤ãƒ«ç’°å¢ƒã§ã¯ /27 ä»¥ä¸Šã®ã‚µã‚¤ã‚ºãŒå¿…è¦ã¨ãªã‚Šã¾ã™ã€‚

## Azure Firewall ã¨ãƒ•ã‚¡ã‚¤ã‚¢ã‚¦ã‚©ãƒ¼ãƒ«ãƒãƒªã‚·ãƒ¼

Azure Firewall ã¯ã€ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã®ãƒˆãƒ©ãƒ•ã‚£ãƒƒã‚¯ã‚’åˆ¶å¾¡ã™ã‚‹ãŸã‚ã®ã‚¯ãƒ©ã‚¦ãƒ‰ãƒã‚¤ãƒ†ã‚£ãƒ–ãªãƒ•ã‚¡ã‚¤ã‚¢ã‚¦ã‚©ãƒ¼ãƒ«ã‚µãƒ¼ãƒ“ã‚¹ã§ã™ã€‚ã“ã®æ§‹æˆã§ã¯ Standard SKU ã‚’ä½¿ç”¨ã—ã€ãƒ•ã‚¡ã‚¤ã‚¢ã‚¦ã‚©ãƒ¼ãƒ«ãƒãƒªã‚·ãƒ¼ã§ãƒ«ãƒ¼ãƒ«ã‚’å®šç¾©ã—ã¦ã„ã¾ã™ã€‚

```bicep
module firewallPolicy 'br/public:avm/res/network/firewall-policy:0.3.4' = {
  name: 'firewallPolicyDeployment'
  params: {
    // Required parameters
    name: azfwpName
    // Non-required parameters
    allowSqlRedirect: true
    snat: {
      autoLearnPrivateRanges: 'Enabled'
    }
    location: location
    enableTelemetry: true
    enableProxy: true
    ruleCollectionGroups: [
      // çœç•¥
    ]
    tier: 'Standard'
  }
}
```

ãƒ•ã‚¡ã‚¤ã‚¢ã‚¦ã‚©ãƒ¼ãƒ«ãƒãƒªã‚·ãƒ¼ã«ã¯ 2 ã¤ã®ãƒ«ãƒ¼ãƒ«ã‚³ãƒ¬ã‚¯ã‚·ãƒ§ãƒ³ã‚°ãƒ«ãƒ¼ãƒ—ã‚’å®šç¾©ã—ã¦ã„ã¾ã™ã€‚

1 ã¤ç›®ã® outbound ã‚°ãƒ«ãƒ¼ãƒ—ã§ã¯ã€Container Apps ã‚µãƒ–ãƒãƒƒãƒˆã‹ã‚‰ã®ã‚¢ã‚¦ãƒˆãƒã‚¦ãƒ³ãƒ‰ãƒˆãƒ©ãƒ•ã‚£ãƒƒã‚¯ã‚’åˆ¶å¾¡ã—ã¾ã™ã€‚HTTP/HTTPS ã®ã‚¢ã‚¦ãƒˆãƒã‚¦ãƒ³ãƒ‰ã‚’è¨±å¯ã™ã‚‹ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ãƒ«ãƒ¼ãƒ«ã¨ã€Azure ã®ã‚µãƒ¼ãƒ“ã‚¹ã‚¿ã‚° (MicrosoftContainerRegistryã€AzureContainerRegistry ãªã©) ã¸ã®æ¥ç¶šã‚’è¨±å¯ã™ã‚‹ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ãƒ«ãƒ¼ãƒ«ã‚’è¨­å®šã—ã¦ã„ã¾ã™ã€‚

2 ã¤ç›®ã® minecraft-server ã‚°ãƒ«ãƒ¼ãƒ—ã§ã¯ã€Minecraft ã‚µãƒ¼ãƒãƒ¼ã¸ã®ã‚¤ãƒ³ãƒã‚¦ãƒ³ãƒ‰ãƒˆãƒ©ãƒ•ã‚£ãƒƒã‚¯ã‚’åˆ¶å¾¡ã—ã¾ã™ã€‚ã“ã“ã§é‡è¦ãªã®ã¯ DNAT ãƒ«ãƒ¼ãƒ«ã®è¨­å®šã§ã™ã€‚

```bicep
{
  ruleCollectionType: 'FirewallPolicyNatRuleCollection'
  action: {
    type: 'Dnat'
  }
  rules: [
    {
      ruleType: 'NatRule'
      name: 'minecraft-server'
      translatedAddress: managedEnvironment.outputs.staticIp
      translatedPort: '25565'
      ipProtocols: [
        'TCP'
      ]
      sourceAddresses: [
        '*'
      ]
      destinationAddresses: [
        fwpip.outputs.ipAddress
      ]
      destinationPorts: [
        '25565'
      ]
    }
  ]
  name: 'nat-minecraft-server'
  priority: 100
}
```

DNAT (Destination Network Address Translation) ãƒ«ãƒ¼ãƒ«ã«ã‚ˆã‚Šã€Azure Firewall ã®ãƒ‘ãƒ–ãƒªãƒƒã‚¯ IP ã‚¢ãƒ‰ãƒ¬ã‚¹å®›ã® 25565 ç•ªãƒãƒ¼ãƒˆã¸ã®ãƒˆãƒ©ãƒ•ã‚£ãƒƒã‚¯ãŒã€Container Apps ç’°å¢ƒã®é™çš„ IP ã‚¢ãƒ‰ãƒ¬ã‚¹ã«è»¢é€ã•ã‚Œã¾ã™ã€‚ãªãŠã€25565 ã¯ Minecraft Java Edition ã‚µãƒ¼ãƒãƒ¼ã®ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆãƒãƒ¼ãƒˆã§ã™ã€‚

## Storage Account ã¨ Private Endpoint

Minecraft ã‚µãƒ¼ãƒãƒ¼ã®ãƒ¯ãƒ¼ãƒ«ãƒ‰ãƒ‡ãƒ¼ã‚¿ã‚’æ°¸ç¶šåŒ–ã™ã‚‹ãŸã‚ã«ã€Azure Files ã‚’ä½¿ç”¨ã—ã¾ã™ã€‚Container Apps ã®ã‚³ãƒ³ãƒ†ãƒŠã¯æœ¬æ¥ã‚¹ãƒ†ãƒ¼ãƒˆãƒ¬ã‚¹ã§ã‚ã‚Šã€å†èµ·å‹•æ™‚ã«ãƒ‡ãƒ¼ã‚¿ãŒå¤±ã‚ã‚Œã‚‹ãŸã‚ã€å¤–éƒ¨ã‚¹ãƒˆãƒ¬ãƒ¼ã‚¸ã¸ã®ãƒ‡ãƒ¼ã‚¿ä¿å­˜ãŒå¿…è¦ã¨ãªã‚Šã¾ã™ã€‚

```bicep
module storageAccount 'br/public:avm/res/storage/storage-account:0.30.0' = {
  name: '${time}-storageAccountDeployment'
  params: {
    name: storageAccountName
    fileServices: {
      shares: [
        {
          accessTier: 'Hot'
          name: 'mcjavashare'
          shareQuota: 5120
        }
      ]
    }
    networkAcls: {
      bypass: 'AzureServices'
      defaultAction: 'Deny'
    }
    privateEndpoints: [
      {
        privateDnsZoneGroup: {
          privateDnsZoneGroupConfigs: [
            {
              privateDnsZoneResourceId: pdnssto.outputs.resourceId
            }
          ]
        }
        service: 'file'
        subnetResourceId: vnet.outputs.subnetResourceIds[1]
      }
    ]
    skuName: 'Standard_ZRS'
  }
}
```

Storage Account ã¯ `networkAcls.defaultAction: 'Deny'` ã«ã‚ˆã‚Šã€ãƒ‘ãƒ–ãƒªãƒƒã‚¯ã‚¢ã‚¯ã‚»ã‚¹ã‚’ãƒ‡ãƒ•ã‚©ãƒ«ãƒˆã§æ‹’å¦ã™ã‚‹è¨­å®šã¨ã—ã¦ã„ã¾ã™ã€‚Container Apps ã‹ã‚‰ã®ã‚¢ã‚¯ã‚»ã‚¹ã¯ Private Endpoint çµŒç”±ã§è¡Œã„ã¾ã™ã€‚

Private Endpoint ã‚’ä½œæˆã™ã‚‹ã¨ã€ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯å†…ã‹ã‚‰ã¯ Storage Account ã®ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆ IP ã‚¢ãƒ‰ãƒ¬ã‚¹ã§ã‚¢ã‚¯ã‚»ã‚¹ã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã™ã€‚ã“ã®åå‰è§£æ±ºã‚’è¡Œã†ãŸã‚ã«ã€ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆ DNS ã‚¾ãƒ¼ãƒ³ `privatelink.file.core.windows.net` ã‚’ä½œæˆã—ã€ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã«ãƒªãƒ³ã‚¯ã—ã¦ã„ã¾ã™ã€‚

## Container Apps ç’°å¢ƒ

Container Apps ç’°å¢ƒã¯ã€ã‚³ãƒ³ãƒ†ãƒŠã‚¢ãƒ—ãƒªã‚’å®Ÿè¡Œã™ã‚‹ãŸã‚ã®åˆ†é›¢ã•ã‚ŒãŸå¢ƒç•Œã‚’æä¾›ã—ã¾ã™ã€‚ã“ã®æ§‹æˆã§ã¯å†…éƒ¨ãƒ¢ãƒ¼ãƒ‰ (internal: true) ã§ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã€ã‚¤ãƒ³ã‚¿ãƒ¼ãƒãƒƒãƒˆã‹ã‚‰ã®ç›´æ¥ã‚¢ã‚¯ã‚»ã‚¹ã‚’é®æ–­ã—ã¦ã„ã¾ã™ã€‚

```bicep
module managedEnvironment 'br/public:avm/res/app/managed-environment:0.8.1' = {
  name: '${time}-managedEnvironmentDeployment'
  params: {
    // Required parameters
    name: mngEnvName
    // Non-required parameters
    appLogsConfiguration: {
      destination: 'log-analytics'
      logAnalyticsConfiguration: {
        customerId: workspace.outputs.logAnalyticsWorkspaceId
        sharedKey: workspace.outputs.primarySharedKey
      }
    }
    infrastructureSubnetResourceId: vnet.outputs.subnetResourceIds[2]
    internal: true
    location: location
    storages: [
      {
        accessMode: 'ReadWrite'
        storageAccountName: storageAccount.outputs.name
        shareName: 'mcjavashare'
        kind: 'SMB'
      }
    ]
    platformReservedCidr: '172.17.17.0/24'
    platformReservedDnsIP: '172.17.17.17'
    workloadProfiles: [
      {
        maximumCount: 1
        minimumCount: 0
        name: 'CAW01'
        workloadProfileType: 'D4'
      }
    ]
  }
}
```

`storages` ãƒ‘ãƒ©ãƒ¡ãƒ¼ã‚¿ã§ Azure Files å…±æœ‰ã‚’ãƒã‚¦ãƒ³ãƒˆã—ã€Container Apps ã‹ã‚‰åˆ©ç”¨ã§ãã‚‹ã‚ˆã†ã«ã—ã¦ã„ã¾ã™ã€‚`kind: 'SMB'` ã¯ SMB ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã§ã®ãƒã‚¦ãƒ³ãƒˆã‚’æ„å‘³ã—ã¾ã™ã€‚

`workloadProfiles` ã§ã¯ D4 ã‚¿ã‚¤ãƒ—ã®ãƒ¯ãƒ¼ã‚¯ãƒ­ãƒ¼ãƒ‰ãƒ—ãƒ­ãƒ•ã‚¡ã‚¤ãƒ«ã‚’å®šç¾©ã—ã¦ã„ã¾ã™ã€‚Minecraft ã‚µãƒ¼ãƒãƒ¼ã¯ã‚ã‚‹ç¨‹åº¦ã®ãƒ¡ãƒ¢ãƒªã¨ CPU ã‚’å¿…è¦ã¨ã™ã‚‹ãŸã‚ã€å¾“é‡èª²é‡‘ã® Consumption ãƒ—ãƒ©ãƒ³ã§ã¯ãªãã€Dedicated ãƒ—ãƒ©ãƒ³ã®ãƒ¯ãƒ¼ã‚¯ãƒ­ãƒ¼ãƒ‰ãƒ—ãƒ­ãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½¿ç”¨ã—ã¦ã„ã¾ã™ã€‚

## Minecraft Container App

æœ€å¾Œã«ã€Minecraft ã‚µãƒ¼ãƒãƒ¼æœ¬ä½“ã® Container App ã‚’å®šç¾©ã—ã¾ã™ã€‚

```bicep
module minecraft 'br/public:avm/res/app/container-app:0.12.0' = {
  name: '${time}-${cappsName}'
  params: {
    name: cappsName
    containers: [
      {
        image: 'docker.io/itzg/minecraft-server'
        name: 'minecraft-server'
        resources: {
          cpu: json('2')
          memory: '4Gi'
        }
        volumeMounts: [
          {
            volumeName: 'mcjavashare'
            mountPath: '/data'
          }
        ]
        env: [
          { name: 'EULA', value: 'true' }
          { name: 'MEMORY', value: '3G' }
          { name: 'OPS', value: 'mattffffff' }
          { name: 'VERSION', value: '1.21.7' }
          { name: 'VIEW_DISTANCE', value: '16' }
        ]
      }
    ]
    volumes: [
      {
        name: 'mcjavashare'
        storageName: 'mcjavashare'
        storageType: 'AzureFile'
      }
    ]
    ingressTargetPort: 25565
    workloadProfileName: 'CAW01'
    environmentResourceId: managedEnvironment.outputs.resourceId
    scaleMinReplicas: 1
    scaleMaxReplicas: 1
    exposedPort: 25565
    ingressTransport: 'tcp'
  }
}
```

ä½¿ç”¨ã—ã¦ã„ã‚‹ã‚³ãƒ³ãƒ†ãƒŠã‚¤ãƒ¡ãƒ¼ã‚¸ã¯ `itzg/minecraft-server` ã§ã‚ã‚Šã€Docker Hub ã§å…¬é–‹ã•ã‚Œã¦ã„ã‚‹å®šç•ªã® Minecraft ã‚µãƒ¼ãƒãƒ¼ã‚¤ãƒ¡ãƒ¼ã‚¸ã§ã™ã€‚ç’°å¢ƒå¤‰æ•°ã§ Minecraft ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã€ãƒ¡ãƒ¢ãƒªå‰²ã‚Šå½“ã¦ã€ã‚ªãƒšãƒ¬ãƒ¼ã‚¿ãƒ¼æ¨©é™ã‚’æŒã¤ãƒ—ãƒ¬ã‚¤ãƒ¤ãƒ¼åãªã©ã‚’è¨­å®šã§ãã¾ã™ã€‚

`volumeMounts` ã§ Azure Files å…±æœ‰ã‚’ `/data` ã«ãƒã‚¦ãƒ³ãƒˆã—ã¦ã„ã¾ã™ã€‚Minecraft ã‚µãƒ¼ãƒãƒ¼ã®ãƒ¯ãƒ¼ãƒ«ãƒ‰ãƒ‡ãƒ¼ã‚¿ã‚„è¨­å®šãƒ•ã‚¡ã‚¤ãƒ«ã¯ /data ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªã«ä¿å­˜ã•ã‚Œã‚‹ãŸã‚ã€ã‚³ãƒ³ãƒ†ãƒŠãŒå†èµ·å‹•ã—ã¦ã‚‚ãƒ‡ãƒ¼ã‚¿ãŒæ°¸ç¶šåŒ–ã•ã‚Œã¾ã™ã€‚

`ingressTransport: 'tcp'` ã¨ `exposedPort: 25565` ã«ã‚ˆã‚Šã€TCP é€šä¿¡ã‚’å—ã‘ä»˜ã‘ã‚‹è¨­å®šã¨ã—ã¦ã„ã¾ã™ã€‚é€šå¸¸ã® Container Apps ã¯ HTTP/HTTPS ã®ã¿ã‚’å—ã‘ä»˜ã‘ã¾ã™ãŒã€TCP ingress ã‚’æœ‰åŠ¹ã«ã™ã‚‹ã“ã¨ã§ Minecraft ã®ãƒ—ãƒ­ãƒˆã‚³ãƒ«ã«ã‚‚å¯¾å¿œã§ãã¾ã™ã€‚

Bicep ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã®å…¨ä½“ã¯ã“ã¡ã‚‰ã€‚

::::details Bicep ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆ
```bicep
@description('ShortName is required for a unique storage account name. Only 5 characters.')
param shortName string

@description('Address prefixes for the virtual network.')
param vnetAddressPrefixes array = [
  '192.168.1.0/24'
]

@description('Name of the Azure Firewall subnet.')
param subnetAzureFirewallName string = 'AzureFirewallSubnet'

@description('Address prefix for the Azure Firewall subnet.')
param subnetAzureFirewallPrefix string = '192.168.1.0/26'

@description('Name of the storage subnet.')
param subnetStorageName string = 'storage'

@description('Address prefix for the storage subnet.')
param subnetStoragePrefix string = '192.168.1.64/27'

@description('Name of the web subnet.')
param subnetWebName string = 'app'

@description('Address prefix for the web subnet.')
param subnetWebPrefix string = '192.168.1.96/27'

@description('Name of the Azure Firewall management subnet.')
param subnetAzureFirewallManagementName string = 'AzureFirewallManagementSubnet'

@description('Address prefix for the Azure Firewall management subnet.')
param subnetAzureFirewallManagementPrefix string = '192.168.1.128/26'

@description('Private DNS zone name for the storage account.')
param pdnsName string = 'privatelink.file.core.windows.net'

@description('Blob name for the storage account.')
param blobName string = 'mcjavablob'

@description('Location for all resources.')
param location string = resourceGroup().location

@description('Current UTC time, used for resource naming.')
param time string = utcNow()

var mngEnvName = 'cenv-mcjava-${shortName}'
var cappsName = 'capp-mcjava-${shortName}'
var vnetName = 'vnet-mcjava-${shortName}'
var storageAccountName = 'mcjavafiles${shortName}'
var workspaceName = 'law-mcjava-${shortName}'
var cenvpipName = 'cenvpip-mcjava-${shortName}'
var azfwName = 'azfw-mcjava-${shortName}'
var azfwpName = 'azfwp-mcjava-${shortName}'
var azfwpipName = 'azfwpip-mcjava-${shortName}'

// Virtual Network (AVM Module)
module vnet 'br/public:avm/res/network/virtual-network:0.7.2' = {
  name: '${time}-privateVnet'
  params: {
    name: vnetName
    addressPrefixes: vnetAddressPrefixes
    subnets: [
      {
        name: subnetAzureFirewallName
        addressPrefix: subnetAzureFirewallPrefix
      }
      {
        name: subnetStorageName
        addressPrefix: subnetStoragePrefix
        privateLinkServiceNetworkPolicies: 'Enabled'
        defaultOutboundAccess: false
      }
      {
        name: subnetWebName
        addressPrefix: subnetWebPrefix
        delegation: 'Microsoft.App/environments'
      }
      {
        name: subnetAzureFirewallManagementName
        addressPrefix: subnetAzureFirewallManagementPrefix
      }
    ]
  }
}

// Private DNS Zone for Storage Account (AVM Module)
module pdnssto 'br/public:avm/res/network/private-dns-zone:0.8.0' = {
  name: '${time}-storagedns'
  params: {
    name: pdnsName
    virtualNetworkLinks: [
      {
        virtualNetworkResourceId: vnet.outputs.resourceId
      }
    ]
  }
}

// Azure Firewall Public IP (AVM Module)
module fwpip 'br/public:avm/res/network/public-ip-address:0.10.0' = {
  name: '${time}-azfwpipDeployment'
  params: {
    // Required parameters
    name: azfwpipName
    // Non-required parameters

    diagnosticSettings: []
    publicIPAddressVersion: 'IPv4'
    publicIPAllocationMethod: 'Static'
    roleAssignments: []
    skuName: 'Standard'
    skuTier: 'Regional'
  }
}

// Azure Firewall (AVM Module)
module azfw 'br/public:avm/res/network/azure-firewall:0.9.2' = {
  name: '${time}-azureFirewallDeployment'
  params: {
    // Required parameters
    name: azfwName
    azureSkuTier: 'Standard'
    virtualNetworkResourceId: vnet.outputs.resourceId
    location: location
    threatIntelMode: 'Alert'
    firewallPolicyId: firewallPolicy.outputs.resourceId
    publicIPResourceID: fwpip.outputs.resourceId
  }
}

// Firewall Policy (AVM Module)
module firewallPolicy 'br/public:avm/res/network/firewall-policy:0.3.4' = {
  name: 'firewallPolicyDeployment'
  params: {
    // Required parameters
    name: azfwpName
    // Non-required parameters
    allowSqlRedirect: true
    snat: {
      autoLearnPrivateRanges: 'Enabled'
    }
    location: location
    enableTelemetry: true
    enableProxy: true
    ruleCollectionGroups: [
      {
        priority: 1000
        name: 'outbound'
        ruleCollections: [
          {
            ruleCollectionType: 'FirewallPolicyFilterRuleCollection'
            action: {
              type: 'Allow'
            }
            rules: [
              {
                ruleType: 'ApplicationRule'
                name: 'vnet-outbound'
                protocols: [
                  {
                    protocolType: 'Https'
                    port: 443
                  }
                  {
                    protocolType: 'Http'
                    port: 80
                  }
                ]
                fqdnTags: []
                webCategories: []
                targetFqdns: [
                  '*'
                ]
                targetUrls: []
                terminateTLS: false
                sourceAddresses: [
                  subnetWebPrefix
                ]
                destinationAddresses: []
                sourceIpGroups: []
                httpHeadersToInsert: []
              }
            ]
            name: 'vnet-outbound'
            priority: 300
          }
          {
            ruleCollectionType: 'FirewallPolicyFilterRuleCollection'
            action: {
              type: 'Allow'
            }
            rules: [
              {
                ruleType: 'NetworkRule'
                name: 'nrc-containerapp-out'
                ipProtocols: [
                  'TCP'
                  'UDP'
                ]
                sourceAddresses: [
                  subnetWebPrefix
                ]
                sourceIpGroups: []
                destinationAddresses: [
                  'MicrosoftContainerRegistry'
                  'AzureFrontDoorFirstParty'
                  'AzureContainerRegistry'
                  'AzureActiveDirectory'
                  'AzureKeyVault'
                ]
                destinationIpGroups: []
                destinationFqdns: []
                destinationPorts: [
                  '80'
                  '443'
                ]
              }
            ]
            name: 'container-app-outbound'
            priority: 400
          }
        ]
      }
      {
        priority: 1100
        name: 'minecraft-server'
        ruleCollections: [
          {
            ruleCollectionType: 'FirewallPolicyFilterRuleCollection'
            action: {
              type: 'Allow'
            }
            rules: [
              {
                ruleType: 'NetworkRule'
                name: 'nrc-minecraft-server-in'
                ipProtocols: [
                  'TCP'
                ]
                sourceAddresses: [
                  '0.0.0.0/0'
                ]
                sourceIpGroups: []
                destinationAddresses: [
                  managedEnvironment.outputs.staticIp
                ]
                destinationIpGroups: []
                destinationFqdns: []
                destinationPorts: [
                  '25565'
                ]
              }
            ]
            name: 'minecraft-server-in'
            priority: 200
          }
          {
            ruleCollectionType: 'FirewallPolicyNatRuleCollection'
            action: {
              type: 'Dnat'
            }
            rules: [
              {
                ruleType: 'NatRule'
                name: 'minecraft-server'
                translatedAddress: managedEnvironment.outputs.staticIp
                translatedPort: '25565'
                ipProtocols: [
                  'TCP'
                ]
                sourceAddresses: [
                  '*'
                ]
                sourceIpGroups: []
                destinationAddresses: [
                  fwpip.outputs.ipAddress
                ]
                destinationPorts: [
                  '25565'
                ]
              }
            ]
            name: 'nat-minecraft-server'
            priority: 100
          }
        ]
      }
    ]
    tier: 'Standard'
  }
}

// Log Analytics Workspace (AVM Module) with Saved Searches
module workspace 'br/public:avm/res/operational-insights/workspace:0.9.1' = {
  name: '${time}-workspaceDeployment'
  params: {
    // Required parameters
    name: workspaceName
    // Non-required parameters
    location: location
  }
}

// Storage Account (AVM Module)
module storageAccount 'br/public:avm/res/storage/storage-account:0.30.0' = {
  name: '${time}-storageAccountDeployment'
  params: {
    // Required parameters
    name: storageAccountName
    // Non-required parameters
    allowSharedKeyAccess: true
    allowBlobPublicAccess: true
    blobServices: {
      automaticSnapshotPolicyEnabled: true
      containerDeleteRetentionPolicyDays: 10
      containerDeleteRetentionPolicyEnabled: true
      containers: [
        {
          enableNfsV3AllSquash: true
          enableNfsV3RootSquash: true
          name: blobName
          publicAccess: 'None'
        }
      ]
      deleteRetentionPolicyDays: 9
      deleteRetentionPolicyEnabled: true
      diagnosticSettings: [
        {
          metricCategories: [
            {
              category: 'AllMetrics'
            }
          ]
          name: 'customSetting'

          workspaceResourceId: workspace.outputs.resourceId
        }
      ]
      lastAccessTimeTrackingPolicyEnabled: true
    }
    diagnosticSettings: [
      {
        metricCategories: [
          {
            category: 'AllMetrics'
          }
        ]
        name: 'customSetting'

        workspaceResourceId: workspace.outputs.resourceId
      }
    ]
    enableHierarchicalNamespace: true
    enableNfsV3: true
    enableSftp: true
    fileServices: {
      diagnosticSettings: [
        {
          metricCategories: [
            {
              category: 'AllMetrics'
            }
          ]
          name: 'customSetting'
          workspaceResourceId: workspace.outputs.resourceId
        }
      ]
      shares: [
        {
          accessTier: 'Hot'
          name: 'mcjavashare'
          shareQuota: 5120
        }
      ]
    }
    largeFileSharesState: 'Enabled'
    localUsers: []
    location: location
    managedIdentities: {
      systemAssigned: true
    }
    managementPolicyRules: []
    networkAcls: {
      bypass: 'AzureServices'
      defaultAction: 'Deny'
      ipRules: []
      virtualNetworkRules: []
    }
    privateEndpoints: [
      {
        privateDnsZoneGroup: {
          privateDnsZoneGroupConfigs: [
            {
              privateDnsZoneResourceId: pdnssto.outputs.resourceId
            }
          ]
        }
        service: 'file'
        subnetResourceId: vnet.outputs.subnetResourceIds[1]
      }
    ]
    requireInfrastructureEncryption: true
    sasExpirationPeriod: '180.00:00:00'
    skuName: 'Standard_ZRS'
    tags: {
      SecurityControl : 'Ignore'
    }
  }
}

// Container Apps Managed Environment (AVM Module)
module managedEnvironment 'br/public:avm/res/app/managed-environment:0.8.1' = {
  name: '${time}-managedEnvironmentDeployment'
  params: {
    // Required parameters
    name: mngEnvName
    logAnalyticsWorkspaceResourceId: workspace.outputs.resourceId
    // Non-required parameters
    infrastructureSubnetId: vnet.outputs.subnetResourceIds[2]
    internal: true
    location: location
    storages: [
      {
        accessMode: 'ReadWrite'
        storageAccountName: storageAccount.outputs.name
        shareName: 'mcjavashare'
        kind: 'SMB'
      }
    ]
    platformReservedCidr: '172.17.17.0/24'
    platformReservedDnsIP: '172.17.17.17'
    workloadProfiles: [
      {
        maximumCount: 1
        minimumCount: 0
        name: 'CAW01'
        workloadProfileType: 'D4'
      }
    ]
  }
}

// Minecraft Container App (AVM Module)
module minecraft 'br/public:avm/res/app/container-app:0.12.0' = {
  name: '${time}-${cappsName}'
  params: {
    name: cappsName
    containers: [
      {
        image: 'docker.io/itzg/minecraft-server'
        name: 'minecraft-server'
        resources: {
          cpu: json('2')
          memory: '4Gi'
        }
        volumeMounts: [
          {
            volumeName: 'mcjavashare'
            mountPath: '/data'
          }
        ]
        env: [
          { name: 'EULA', value: 'true' }
          { name: 'MEMORY', value: '3G' }
          { name: 'OPS', value: 'mattffffff' }
          { name: 'VERSION', value: '1.21.11' }
          { name: 'VIEW_DISTANCE', value: '16' }
        ]
      }
    ]
    volumes: [
      {
        name: 'mcjavashare'
        storageName: 'mcjavashare'
        storageType: 'AzureFile'
      }
    ]
    ingressTargetPort: 25565
    workloadProfileName: 'CAW01'
    environmentResourceId: managedEnvironment.outputs.resourceId
    scaleMinReplicas: 1
    scaleMaxReplicas: 1
    exposedPort: 25565
    ingressTransport: 'tcp'
    trafficWeight: 100
    trafficLatestRevision: true
  }
}

// Output the Minecraft server endpoint
output minecraftEndpoint string = fwpip.outputs.ipAddress
```
::::

## ãƒ‡ãƒ—ãƒ­ã‚¤ã¨æ¥ç¶š

ã“ã®ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã‚’ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹ã¨ã€å‡ºåŠ›ã¨ã—ã¦ Azure Firewall ã®ãƒ‘ãƒ–ãƒªãƒƒã‚¯ IP ã‚¢ãƒ‰ãƒ¬ã‚¹ãŒè¿”ã•ã‚Œã¾ã™ã€‚

```bicep
output minecraftEndpoint string = fwpip.outputs.ipAddress
```

Minecraft ã‚¯ãƒ©ã‚¤ã‚¢ãƒ³ãƒˆã‹ã‚‰ã¯ã€ã“ã®ãƒ‘ãƒ–ãƒªãƒƒã‚¯ IP ã‚¢ãƒ‰ãƒ¬ã‚¹ã«å¯¾ã—ã¦ãƒãƒ¼ãƒˆ 25565 ã§æ¥ç¶šã™ã‚‹ã“ã¨ã§ã‚µãƒ¼ãƒãƒ¼ã«å‚åŠ ã§ãã¾ã™ã€‚

![alt text](/images/5bed15e0ba0036/mc5.png)

## ç®¡ç†

ã“ã‚Œã§æ§‹ç¯‰ã—ã¦éŠã¹ã‚‹ã¾ã§ã«ã¯ãªã‚Šã¾ã—ãŸã€‚
ã“ã“ã‹ã‚‰ã¯ç®¡ç†é¢ã§ã®ãƒˆãƒ”ãƒƒã‚¯ã‚’æŒ™ã’ã¦ã„ãã¾ã™ã€‚

### ãƒãƒƒã‚¯ã‚¢ãƒƒãƒ—

ä»Šå›ã®æ§‹æˆã§ã¯ã€Minecraft ã‚µãƒ¼ãƒãƒ¼ã®ãƒ¯ãƒ¼ãƒ«ãƒ‰ãƒ‡ãƒ¼ã‚¿ã¯ Azure Files ã«ä¿å­˜ã•ã‚Œã¾ã™ã€‚
ã‚µãƒ¼ãƒãƒ¼ã®ãŠå¼•è¶Šã—ãŒå¿…è¦ãªæ™‚ã¯ã€ã“ã¡ã‚‰ã®ãƒ‡ãƒ¼ã‚¿ã‚’ç§»è¡Œã—ã¾ã—ã‚‡ã†ã€‚

Azure Portal ã‚„ Azure CLI ã‚’ä½¿ç”¨ã—ã¦æ‰‹å‹•ã§ãƒãƒƒã‚¯ã‚¢ãƒƒãƒ—ã‚’å–å¾—ã§ãã¾ã™ã€‚ã¾ãŸã€Azure Backup ã‚µãƒ¼ãƒ“ã‚¹ã‚’åˆ©ç”¨ã—ã¦è‡ªå‹•ãƒãƒƒã‚¯ã‚¢ãƒƒãƒ—ã‚’è¨­å®šã™ã‚‹ã“ã¨ã‚‚å¯èƒ½ã§ã™ã€‚

### ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã‚¢ãƒƒãƒ—

Minecraft ã¯å¹´ã«ï¼‘åº¦ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã‚¢ãƒƒãƒ—ã‹ã‚‰ã€å¹´ã«è¤‡æ•°å›ã®æ›´æ–°ã‚’è¡Œã†æ–¹é‡ã«å¤‰ã‚ã£ãŸã“ã¨ãŒæ˜¨å¹´ç™ºè¡¨ã•ã‚Œã¾ã—ãŸã€‚ãã®ãŸã‚ã€ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã‚¢ãƒƒãƒ—ã¯å¿…é ˆã¨ãªã‚Šã¾ã™ã€‚

ã“ã®ã‚³ãƒ³ãƒ†ãƒŠã‚¤ãƒ¡ãƒ¼ã‚¸ã§ã¯ã€Minecraft ã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã¨ã—ã¦ã€ç’°å¢ƒå¤‰æ•° ```VERSION``` ã§æŒ‡å®šã—ã¦ã„ã¾ã™ã€‚ã“ã‚Œã«ã‚ˆã£ã¦æŒ‡å®šã•ã‚ŒãŸãƒãƒ¼ã‚¸ãƒ§ãƒ³ã§ Minecraft ã‚µãƒ¼ãƒãƒ¼ãŒèµ·å‹•ã§ãã¾ã™ã€‚

```bicep
        env: [
          { name: 'EULA', value: 'true' }
          { name: 'MEMORY', value: '3G' }
          { name: 'OPS', value: 'mattffffff' }
          { name: 'VERSION', value: '1.21.7' }
          { name: 'VIEW_DISTANCE', value: '16' }
        ]
```

æ³¨æ„ç‚¹ã¨ã—ã¦ã¯ã€Bicep ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã®å¯¾è±¡ç’°å¢ƒå¤‰æ•°ã‚’æ›´æ–°ã—ã¦ã€å†åº¦ Bicep ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã‚’æµã›ã°ã‚ˆã„ã®ã§ã™ãŒã€å‰ã®ãƒªãƒ“ã‚¸ãƒ§ãƒ³ã§ãƒ•ã‚¡ã‚¤ãƒ«å…±æœ‰ã‚’ã¤ã‹ã‚“ã§ã„ã‚‹é–¢ä¿‚ã§ã€æ–°è¦ãƒªãƒ“ã‚¸ãƒ§ãƒ³ã®ã‚³ãƒ³ãƒ†ãƒŠã§ Minecraft ã‚µãƒ¼ãƒãƒ¼ã®èµ·å‹•ãŒå¤±æ•—ã—ã¾ã™ã€‚

![alt text](/images/5bed15e0ba0036/mc6.png)

ãã®å ´åˆã€ä¸€åº¦ Container App ã‚’åœæ­¢ã€ãã®å¾Œèµ·å‹•ã™ã‚‹ã“ã¨ã§æ­£å¸¸ã«ãƒªãƒ“ã‚¸ãƒ§ãƒ³ãŒåæ˜ ã•ã‚Œã‚‹ã‹ã¨æ€ã„ã¾ã™ã€‚

### ç›£è¦–

Log Analytics ãƒ¯ãƒ¼ã‚¯ã‚¹ãƒšãƒ¼ã‚¹ã‚’ Container Apps ç’°å¢ƒã«ç´ã¥ã‘ã‚‹ã“ã¨ã§ã€ã‚³ãƒ³ãƒ†ãƒŠã®ã‚¢ãƒ—ãƒªã‚±ãƒ¼ã‚·ãƒ§ãƒ³ã«ã‹ã‹ã‚ã‚‹ãƒ­ã‚°ã‚’è»¢é€ã§ãã¾ã™ã€‚

ã“ã®ãƒ­ã‚°ã«ã¯ã€Minecraft ã‚µãƒ¼ãƒãƒ¼ã¸ãƒ­ã‚°ã‚¤ãƒ³ã—ãŸãƒ¦ãƒ¼ã‚¶ãƒ¼ã‚„ã€ã‚²ãƒ¼ãƒ å†…ã§å…¥åŠ›ã—ãŸãƒãƒ£ãƒƒãƒˆãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ãŒå«ã¾ã‚Œã¾ã™ã€‚ãŸã¨ãˆã°ã€ä»¥ä¸‹ã® KQL ã‚¯ã‚¨ãƒªã‚’ä½¿ç”¨ã—ã¦ã€Minecraft ã‚µãƒ¼ãƒãƒ¼ã®èµ·å‹•ãŒå®Œäº†ã—ãŸéš›ã®ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸ã‚’å–å¾—ã§ãã¾ã™ã€‚

```kql
ContainerAppConsoleLogs_CL
| where Log_s has "0.0.0.0"
```

![alt text](/images/5bed15e0ba0036/mc7.png)

ã¾ãŸã€Azure Monitor ã«å«ã¾ã‚Œã‚‹ Grafana ãƒ€ãƒƒã‚·ãƒ¥ãƒœãƒ¼ãƒ‰ãŒç”¨æ„ã•ã‚Œã¦ã„ã¾ã™ã€‚
ãã—ã¦ãªã‚“ã¨ç„¡æ–™ã€‚ã–ã£ã¨ãƒ‘ãƒ•ã‚©ãƒ¼ãƒãƒ³ã‚¹ã®å‚¾å‘ã‚’è¦‹ã‚‹ã®ã«ä¾¿åˆ©ã§ã™ã­ã€‚
Minecraft ã‚µãƒ¼ãƒãƒ¼ã®ãƒ‘ãƒ•ã‚©ãƒ¼ãƒãƒ³ã‚¹æˆ¦ç•¥ã¨ã—ã¦ã¯ã‚¹ã‚±ãƒ¼ãƒ«ã‚¢ãƒƒãƒ—ã«ãªã‚‹ã®ã§ã€å‚¾å‘ã‚’ã¤ã‹ã‚“ã§æ—©ã‚ã«å¯¾å‡¦ã§ãã‚‹ã‚ˆã†ã«ãªã‚Šã¾ã—ã‚‡ã†ã€‚

![alt text](/images/5bed15e0ba0036/mc8.png)

### IaC ãƒªã‚½ãƒ¼ã‚¹ã®ãƒ¡ãƒ³ãƒ†ãƒŠãƒ³ã‚¹

AVM ã¯ç¶™ç¶šçš„ã«ã‚¢ãƒƒãƒ—ãƒ‡ãƒ¼ãƒˆã•ã‚Œã¦ã„ã‚‹ãŸã‚ã€æ–°ã—ã„ãƒãƒ¼ã‚¸ãƒ§ãƒ³ãŒãƒªãƒªãƒ¼ã‚¹ã•ã‚ŒãŸéš›ã«ã¯ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã®ãƒãƒ¼ã‚¸ãƒ§ãƒ³ç•ªå·ã‚’æ›´æ–°ã™ã‚‹ã“ã¨ã§ã€æœ€æ–°ã®æ©Ÿèƒ½ã‚„ã‚»ã‚­ãƒ¥ãƒªãƒ†ã‚£æ”¹å–„ã‚’å–ã‚Šè¾¼ã‚ã¾ã™ã€‚Bicep ã® `bicepconfig.json` ã§ `use-recent-module-versions` ã‚’æœ‰åŠ¹ã«ã™ã‚‹ã¨ã€å¤ã„ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã®ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã‚’ä½¿ç”¨ã—ã¦ã„ã‚‹å ´åˆã«è­¦å‘ŠãŒè¡¨ç¤ºã•ã‚Œã‚‹ãŸã‚ã€ã‚¢ãƒƒãƒ—ãƒ‡ãƒ¼ãƒˆã®è¦‹è½ã¨ã—ã‚’é˜²ãã“ã¨ãŒã§ãã¾ã™ [^7]ã€‚

## ã¾ã¨ã‚

ã„ã‹ãŒã§ã—ãŸã§ã—ã‚‡ã†ã‹â›ï¸

æœ¬è¨˜äº‹ã§ã¯ã€Azure Verified Module ã‚’ä½¿ã£ã¦ Minecraft ã‚µãƒ¼ãƒãƒ¼ã®ã‚¤ãƒ³ãƒ•ãƒ©ã‚¹ãƒˆãƒ©ã‚¯ãƒãƒ£ã‚’æ§‹ç¯‰ã™ã‚‹æ–¹æ³•ã‚’è§£èª¬ã—ã¾ã—ãŸã€‚AVM ã‚’ä½¿ç”¨ã™ã‚‹ã“ã¨ã§ã€Microsoft ãŒæ¨å¥¨ã™ã‚‹è¨­å®šãŒé©ç”¨ã•ã‚ŒãŸçŠ¶æ…‹ã§ãƒ‡ãƒ—ãƒ­ã‚¤ã§ãã¾ã™ã€‚ã¾ãŸã€ä¸€ã‹ã‚‰ä½œã‚‹ã‚ˆã‚Šã‚‚é–‹ç™ºåŠ¹ç‡ãŒå¤§å¹…ã«å‘ä¸Šã—ã¾ã™ã€‚

ãœã²ã€Azure ã§ã®æ§‹ç¯‰ã‚’æ¤œè¨ã•ã‚Œã‚‹éš›ã«ã¯ã€ã¾ãš AVM ã«å¯¾å¿œã™ã‚‹ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ãŒãªã„ã‹ç¢ºèªã—ã¦ã¿ã¦ãã ã•ã„ã€‚

[^1]: [è‡ªå®…ãƒã‚¤ã‚¯ãƒ©ã‚µãƒ¼ãƒ ã‚’Azureã«ç§»è¡Œ](https://zenn.dev/yurio/articles/624c3e41e0d979)
[^2]: [Azure Verified Modules documentation](https://learn.microsoft.com/en-us/azure/azure-resource-manager/verified-modules/)
[^3]: [avm-bicep-lab-minecraft](https://github.com/ChrisSidebotham/avm-bicep-lab-minecraft/tree/main)
[^4]: [Azure Well-Architected Framework](https://learn.microsoft.com/en-us/azure/architecture/framework/)
[^5]: [Bicep Contribution Flow](https://azure.github.io/Azure-Verified-Modules/contributing/bicep/bicep-contribution-flow/#5-createupdate-and-run-tests)
[^6]: [Using modules in Bicep](https://learn.microsoft.com/en-us/azure/azure-resource-manager/bicep/modules#module-registry-cache)
[^7]: [ãƒªãƒ³ã‚¿ãƒ¼ ãƒ«ãƒ¼ãƒ« - æœ€æ–°ã®ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ« ãƒãƒ¼ã‚¸ãƒ§ãƒ³ã‚’ä½¿ç”¨ã™ã‚‹](https://learn.microsoft.com/ja-jp/azure/azure-resource-manager/bicep/linter-rule-use-recent-module-versions)