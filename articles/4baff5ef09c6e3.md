---
title: "ãƒ‡ãƒ—ãƒ­ã‚¤ã‚¹ã‚¯ãƒªãƒ—ãƒˆ"
emoji: "ğŸ”§"
type: "tech"
topics: ["Azure", "Bicep", "Azure CLI", "Azure PowerShell"]
published: false
---

# ã¯ã˜ã‚ã«

æ¤œè¨¼ç’°å¢ƒä½œã‚‹æ™‚ã«ãƒ†ã‚¹ãƒˆãƒ‡ãƒ¼ã‚¿ã®æŠ•å…¥ã•ã›ãŸã„å ´åˆãªã©ã§å½¹ç«‹ã¡ãã†ã¨ã„ã†ã“ã¨ã§ãƒ¡ãƒ¢ã€‚

ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆã¯ã€ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆ(Azure Bicep / Azure ARM Template)ã‹ã‚‰Azure CLIã‚‚ã—ãAzure PowerShellã‚’å®Ÿè¡Œã§ãã‚‹æ©Ÿèƒ½ã€‚
ä»•çµ„ã¿ã¨ã—ã¦ã¯ã€Azure CLIã‚‚ã—ãã¯Azure PowerShellãŒã‚¤ãƒ³ã‚¹ãƒˆãƒ¼ãƒ«ã•ã‚ŒãŸã‚³ãƒ³ãƒ†ãƒŠã‚¤ãƒ¡ãƒ¼ã‚¸ã‚’Azure Container Instance (ä»¥é™ã€ACI)ã¨ã—ã¦ãƒ‡ãƒ—ãƒ­ã‚¤ã—ã€ãã“ã‹ã‚‰æŒ‡å®šã•ã‚ŒãŸã‚³ãƒãƒ³ãƒ‰ã‚’å®Ÿè¡Œã™ã‚‹ã¨ã„ã†ã‚‚ã®ã€‚

# ä½¿ã„æ–¹

ã“ã®ä¾‹ã§ã¯ã€Azure ãƒ•ã‚¡ã‚¤ãƒ«å…±æœ‰ã‚’æ§‹ç¯‰ã—ã¦ã€ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆã§ãƒ€ã‚¦ãƒ³ãƒ­ãƒ¼ãƒ‰ã—ãŸjsonãƒ•ã‚¡ã‚¤ãƒ«ã‚’å¯¾è±¡ãƒ•ã‚¡ã‚¤ãƒ«å…±æœ‰ã«ã‚¢ãƒƒãƒ—ãƒ­ãƒ¼ãƒ‰ã™ã‚‹ä¾‹ã€‚

```bicep
var storageAccountName = 'storage${uniqueString(resourceGroup().id)}'
var storagefileShareName = 'config'
var userAssignedIdentityName = 'configDeployer'
var roleAssignmentName = guid(resourceGroup().id, 'contributor')
var contributorRoleDefinitionId = resourceId('Microsoft.Authorization/roleDefinitions', 'b24988ac-6180-42a0-ab88-20f7382dd24c')
var deploymentScriptName = 'CopyConfigScript'
var location = resourceGroup().location

resource storageAccount 'Microsoft.Storage/storageAccounts@2023-01-01' = {
  name: storageAccountName
  tags: {
    displayName: storageAccountName
  }
  location: location
  kind: 'StorageV2'
  sku: {
    name: 'Standard_LRS'
    tier: 'Standard'
  }
  properties: {
    encryption: {
      services: {
        file: {
          enabled: true
        }
      }
      keySource: 'Microsoft.Storage'
    }
    supportsHttpsTrafficOnly: true
  }

  resource fileService 'fileServices' existing = {
    name: 'default'
  }
}

resource fileShare 'Microsoft.Storage/storageAccounts/fileServices/shares@2023-01-01' = {
  parent: storageAccount::fileService
  name: storagefileShareName
  properties: {
    enabledProtocols: 'SMB'
  }
}

resource userAssignedIdentity 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' = {
  name: userAssignedIdentityName
  location: location
}

resource roleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: roleAssignmentName
  properties: {
    roleDefinitionId: contributorRoleDefinitionId
    principalId: userAssignedIdentity.properties.principalId
    principalType: 'ServicePrincipal'
  }
}

resource deploymentScript 'Microsoft.Resources/deploymentScripts@2020-10-01' = {
  name: deploymentScriptName
  location: location
  kind: 'AzurePowerShell'
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${userAssignedIdentity.id}': {}
    }
  }
  properties: {
    azPowerShellVersion: '10.0'
    environmentVariables: [
      {
        name: 'ResourceGroupName'
        value: resourceGroup().name
      }
      {
        name: 'StorageAccountName'
        value: storageAccountName
      }
      {
        name: 'StorageFileShareName'
        value: storagefileShareName
      }
    ]
    scriptContent: '''
      Invoke-RestMethod -Uri 'https://raw.githubusercontent.com/Azure/azure-docs-json-samples/master/mslearn-arm-deploymentscripts-sample/appsettings.json' -Out 'appsettings.json'
      $storageAccount = Get-AzStorageAccount -ResourceGroupName $env:ResourceGroupName -Name $env:StorageAccountName
      Set-AzStorageFileContent -Context $storageAccount.Context -ShareName $env:StorageFileShareName -Source ".\appsettings.json"
      $file = Get-AzStorageFile -Context $storageAccount.Context -ShareName $env:StorageFileShareName
      $DeploymentScriptOutputs = @{}
      $DeploymentScriptOutputs['Uri'] = $file.CloudFile.Uri.AbsoluteUri
    '''
    cleanupPreference: 'OnExpiration'
    retentionInterval: 'P1D'
  }
  dependsOn: [
    roleAssignment
    fileShare
  ]
}

output fileUri string = deploymentScript.properties.outputs.Uri
```

ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹ã€‚

```shell
$ az group create -n labbicep -l japaneast
$ az deployment group create -g labbicep --template-file ./azure-bicep/deployscript/example-setfile.bicep 
```

Azure CLIã®å ´åˆã¯ã“ã¡ã‚‰

```bicep
var storageAccountName = 'storage${uniqueString(resourceGroup().id)}'
var storagefileShareName = 'config'
var userAssignedIdentityName = 'configDeployer'
var roleAssignmentName = guid(resourceGroup().id, 'contributor')
var contributorRoleDefinitionId = resourceId('Microsoft.Authorization/roleDefinitions', 'b24988ac-6180-42a0-ab88-20f7382dd24c')
var deploymentScriptName = 'CopyConfigScript'
var location = resourceGroup().location

resource storageAccount 'Microsoft.Storage/storageAccounts@2023-01-01' = {
  name: storageAccountName
  tags: {
    displayName: storageAccountName
  }
  location: location
  kind: 'StorageV2'
  sku: {
    name: 'Standard_LRS'
    tier: 'Standard'
  }
  properties: {
    encryption: {
      services: {
        file: {
          enabled: true
        }
      }
      keySource: 'Microsoft.Storage'
    }
    supportsHttpsTrafficOnly: true
  }

  resource fileService 'fileServices' existing = {
    name: 'default'
  }
}

resource fileShare 'Microsoft.Storage/storageAccounts/fileServices/shares@2023-01-01' = {
  parent: storageAccount::fileService
  name: storagefileShareName
  properties: {
    enabledProtocols: 'SMB'
  }
}

resource userAssignedIdentity 'Microsoft.ManagedIdentity/userAssignedIdentities@2023-01-31' = {
  name: userAssignedIdentityName
  location: location
}

resource roleAssignment 'Microsoft.Authorization/roleAssignments@2022-04-01' = {
  name: roleAssignmentName
  properties: {
    roleDefinitionId: contributorRoleDefinitionId
    principalId: userAssignedIdentity.properties.principalId
    principalType: 'ServicePrincipal'
  }
}

resource deploymentScript 'Microsoft.Resources/deploymentScripts@2020-10-01' = {
  name: deploymentScriptName
  location: location
  kind: 'AzureCLI'
  identity: {
    type: 'UserAssigned'
    userAssignedIdentities: {
      '${userAssignedIdentity.id}': {}
    }
  }
  properties: {
    azCliVersion: '2.49.0'
    environmentVariables: [
      {
        name: 'ResourceGroupName'
        value: resourceGroup().name
      }
      {
        name: 'StorageAccountName'
        value: storageAccountName
      }
      {
        name: 'StorageFileShareName'
        value: storagefileShareName
      }
    ]
    scriptContent: '''
      wget 'https://raw.githubusercontent.com/Azure/azure-docs-json-samples/master/mslearn-arm-deploymentscripts-sample/appsettings.json' -o 'appsettings.json'
      az storage file upload --account-name $StorageAccountName --share-name $StorageFileShareName --source "appsettings.json" --only-show-errors
      URL=$(az storage file url --account-name $StorageAccountName --share-name $StorageFileShareName -p "appsettings.json" --only-show-errors)
      echo "{\"url\":"$URL", \"storageaccount\":\"$StorageAccountName\", \"fileshare\":\"$StorageFileShareName\"}" > $AZ_SCRIPTS_OUTPUT_PATH
    '''
    cleanupPreference: 'OnExpiration'
    retentionInterval: 'P1D'
  }
  dependsOn: [
    roleAssignment
    fileShare
  ]
}

output fileUri string = deploymentScript.properties.outputs.url
```

ãƒ‡ãƒ—ãƒ­ã‚¤ã™ã‚‹ã€‚

```shell
$ az group create -n labbicep -l japaneast
$ az deployment group create -g labbicep --template-file ./azure-bicep/deployscript/example-setfilecli.bicep 
```

## èªè¨¼

ãƒ‡ãƒ—ãƒ­ã‚¤ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’å®Ÿè¡Œã™ã‚‹éš›ã«ã¯ã€ï¼’ç¨®é¡ã®æ¨©é™ã«ã¤ã„ã¦è€ƒæ…®ã™ã‚‹å¿…è¦ãŒã‚ã‚‹ã€‚

1ã¤ç›®ãŒ"ãƒ‡ãƒ—ãƒ­ã‚¤ ãƒ—ãƒªãƒ³ã‚·ãƒ‘ãƒ«"ã§ã€ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’å®Ÿè¡Œã™ã‚‹ãƒ¦ãƒ¼ã‚¶ãƒ¼ã‚„ã‚µãƒ¼ãƒ“ã‚¹ãƒ—ãƒªãƒ³ã‚·ãƒ‘ãƒ«ã«ã‚ãŸã‚‹ã€‚
ã“ã‚Œã¯ã€Bicepã®ãƒ‡ãƒ—ãƒ­ã‚¤æ¨©é™ã¨åˆã‚ã›ã€ Azure Container Instanceã‚„ã‚¹ãƒˆãƒ¬ãƒ¼ã‚¸ ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’ä½œæˆã™ã‚‹éš›ã®æ¨©é™ã‚’æŒã¤å¿…è¦ãŒã‚ã‚‹

ã‚‚ã†ä¸€æ–¹ãŒã€ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆ ãƒ—ãƒªãƒ³ã‚·ãƒ‘ãƒ«ã§ã€ã“ã‚Œã¯ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆå†…ã§ Azure CLI/Azure PowerShellã‚’å‘¼ã¶éš›ã«ç”¨ã„ã‚‰ã‚Œã‚‹ã€‚
å¤‰æ•°ã¨ã—ã¦ã‚µãƒ¼ãƒ“ã‚¹ ãƒ—ãƒªãƒ³ã‚·ãƒ‘ãƒ«ã‚’æ¸¡ã›ã‚‹ã‘ã©ã€ãƒ¦ãƒ¼ã‚¶ãƒ¼å‰²ã‚Šå½“ã¦ãƒãƒãƒ¼ã‚¸ãƒ‰IDã‚’ç”¨ã„ãŸæ–¹ãŒç°¡å˜ã€‚

ãªãŠã€ã©ã¡ã‚‰ã‚‚çœç•¥ã—ãŸå ´åˆã€ï¼‘ã¤ç›®ã®ãƒ‡ãƒ—ãƒ­ã‚¤ ãƒ—ãƒªãƒ³ã‚·ãƒ‘ãƒ«ãŒä½¿ã‚ã‚Œã‚‹ã“ã¨ã‚’æœŸå¾…ã—ãŸãŒã€
ãã†ã¯ãªã‚‰ãšã€èªè¨¼ã«å¤±æ•—ã—ã¦ã—ã¾ã†ã€‚

```bash
$ az deployment group create -g labbicep --template-file ./azure-bicep/deployscript/example-setblob.bicep 
...(çœç•¥)...
{"status":"Failed","error":{"code":"DeploymentFailed","target":"/subscriptions/xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx/resourceGroups/labbicep/providers/Microsoft.Resources/deployments/example-setblob","message":"At least one resource deployment operation failed. Please list deployment operations for details. Please see https://aka.ms/arm-deployment-operations for usage details.","details":[{"code":"ResourceDeploymentFailure","target":"/subscriptions/xxxxxxxx-xxxx-xxxx-xxxx-xxxxxxxxxxxx/resourceGroups/labbicep/providers/Microsoft.Resources/deploymentScripts/CopyConfigScript","message":"The resource write operation failed to complete successfully, because it reached terminal provisioning state 'failed'.","details":[{"code":"DeploymentScriptError","message":"The provided script failed with multiple errors. First error:\r\nMicrosoft.Azure.Commands.Common.Exceptions.AzPSApplicationException: No subscription found in the context.  Please ensure that the credentials you provided are authorized to access an Azure subscription, then run Connect-AzAccount to login.
```

## å‡ºåŠ›ã®å—ã‘æ¸¡ã—

PowerShellã®å ´åˆã¯ã€å®Ÿè¡Œã™ã‚‹ã‚¹ã‚¯ãƒªãƒ—ãƒˆå†…ã§ã¯ã€é€£æƒ³é…åˆ— ```$DeploymentScriptOutputs``` ã‚’å®šç¾©ã—ã€
ãã“ã«å‡ºåŠ›ã«å‡ºã—ãŸã„å€¤ã‚’å…¥ã‚Œã¦ã‚ã’ã‚‹ã€‚

```powershell
      $DeploymentScriptOutputs = @{}
      $DeploymentScriptOutputs['Uri'] = $file.CloudFile.Uri.AbsoluteUri
```

Bicepã®æ–¹ã§ã¯ã€å–ã‚Šå‡ºã—ãŸã„å‡ºåŠ›åˆ† ```output``` ã§å®šç¾©ã—ã€
Azure CLIã‹ã‚‰Bicepãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã‚’å®Ÿè¡Œã—ãŸå ´åˆã€ä»¥ä¸‹ã®ã‚ˆã†ã«å‚ç…§ã§ãã‚‹ã€‚

```bicep
output fileUri string = deploymentScript.properties.outputs.Uri
```

```shell
$ az deployment group show --resource-group labbicep --name 'example-setfile' --query 'properties.outputs.fileUri.value' --output tsv
https://xxx.file.core.windows.net/config/appsettings.json
```

ã‚‚ã—ãã¯

```shell
$ az deployment-scripts show --resource-group 'labbicep' --name 'CopyConfigScript' --query 'outputs.Uri' -o tsv
https://xxx.file.core.windows.net/config/appsettings.json
```

Azure CLIã®å ´åˆã«ã¯PowerShellã®ã‚ˆã†ã«é€£æƒ³é…åˆ—ã®ç”¨æ„ãŒãªãã€```$AZ_SCRIPTS_OUTPUT_PATH```ã«ãƒªãƒ€ã‚¤ãƒ¬ã‚¯ãƒˆã™ã‚‹ã€‚
ã“ã®ç’°å¢ƒå¤‰æ•°ã¯ã€```/mnt/azscripts/azscriptoutput/scriptoutputs.json```ã«ãªã£ã¦ã„ã¦ã€ã“ã‚ŒãŒãƒ‘ãƒ¼ã‚¹ã•ã‚Œã‚‹ã¿ãŸã„ã€‚

ãƒ‰ã‚­ãƒ¥ãƒ¡ãƒ³ãƒˆã®ä¾‹ã§ã¯ã€é…åˆ—ã§ç¢ºèªã—ã¦ã„ã‚‹ãŒã€ãã†ã§ãªãã¦ã‚‚å–å¾—ã§ããŸã€‚

[CLI ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‹ã‚‰ã®å‡ºåŠ›ã‚’æ“ä½œã™ã‚‹](https://learn.microsoft.com/ja-jp/azure/azure-resource-manager/bicep/deployment-script-bicep#work-with-outputs-from-cli-script)

>ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆã®å‡ºåŠ›ã¯ AZ_SCRIPTS_OUTPUT_PATH ã®å ´æ‰€ã«ä¿å­˜ã•ã‚Œã‚‹å¿…è¦ãŒã‚ã‚Šã€ãã®å‡ºåŠ›ã¯æœ‰åŠ¹ãª JSON æ–‡å­—åˆ—ã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã§ãªã‘ã‚Œã°ãªã‚Šã¾ã›ã‚“ã€‚ 
>ãƒ•ã‚¡ã‚¤ãƒ«ã®å†…å®¹ã¯ã€ã‚­ãƒ¼ã¨å€¤ã®ãƒšã‚¢ã¨ã—ã¦ä¿å­˜ã•ã‚Œã‚‹å¿…è¦ãŒã‚ã‚Šã¾ã™ã€‚ 
>ãŸã¨ãˆã°ã€æ–‡å­—åˆ—ã®é…åˆ—ã¯ã€{ "MyResult": [ "foo", "bar"] } ã¨ã—ã¦æ ¼ç´ã•ã‚Œã¾ã™ã€‚ é…åˆ—ã®çµæœã®ã¿ ([ "foo", "bar" ] ãªã©) ã®æ ¼ç´ã¯ã€ç„¡åŠ¹ã§ã™ã€‚

```bicep
echo "{\"url\":"$URL", \"storageaccount\":\"$StorageAccountName\", \"fileshare\":\"$StorageFileShareName\"}" > $AZ_SCRIPTS_OUTPUT_PATH
```

```bicep
output fileUri string = deploymentScript.properties.outputs.url
```

```shell
$ az deployment group show --resource-group labbicep --name 'example-setfilecli' --query 'properties.outputs.fileUri.value' --output tsv
https://xxx.file.core.windows.net/config/appsettings.json

$ az deployment-scripts show --resource-group 'labbicep' --name 'CopyConfigScript' --query 'outputs.url' -o tsv
https://xxx.file.core.windows.net/config/appsettings.json
```

![Azure ãƒãƒ¼ã‚¿ãƒ«ã‚ˆã‚Šãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆã®å‡ºåŠ›ã‚’ç¢ºèª](/images/4baff5ef09c6e3/img.png)

## åˆ¶é™

ç¾æ™‚ç‚¹ã®åˆ¶é™ã¨ã—ã¦ã¯ã€ä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã«ãƒ‡ãƒ—ãƒ­ã‚¤ãŒã§ããªã„ãŸã‚ã€
ä¾‹ãˆã°ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆ ã‚¨ãƒ³ãƒ‰ãƒã‚¤ãƒ³ãƒˆã«åˆ¶é™ã—ãŸãƒªã‚½ãƒ¼ã‚¹ã¸ã®ã‚¢ã‚¯ã‚»ã‚¹ãŒè¡Œãˆãªã„ã€‚

* [ãƒ—ãƒ©ã‚¤ãƒ™ãƒ¼ãƒˆä»®æƒ³ãƒãƒƒãƒˆãƒ¯ãƒ¼ã‚¯ã¸ã®ã‚¢ã‚¯ã‚»ã‚¹](https://learn.microsoft.com/ja-jp/azure/azure-resource-manager/bicep/deployment-script-bicep#access-private-virtual-network)


## ãƒ­ãƒ¼ã‚«ãƒ«ã§ãƒ†ã‚¹ãƒˆ

ACIã§å‚ç…§ã—ã¦ã„ã‚‹ã‚³ãƒ³ãƒ†ãƒŠã‚¤ãƒ¡ãƒ¼ã‚¸ã¯å…¬é–‹ã•ã‚Œã¦ã„ã‚‹ã®ã§ã€ãƒ‡ãƒ—ãƒ­ã‚¤ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’ä½œæˆã™ã‚‹ã«ã‚ãŸã‚Šã€ãƒ­ãƒ¼ã‚«ãƒ«ã§ã¾ãšã¯è©¦ã—ã¦ã‹ã‚‰è¡Œã†ã€‚
ãŸã ã—ã€ãƒ‡ãƒ—ãƒ­ã‚¤ã‚¹ã‚¯ãƒªãƒ—ãƒˆã§ã¯åˆ©ç”¨ã§ãã‚‹ç’°å¢ƒå¤‰æ•°ã¯ç”¨æ„ã•ã‚Œã¦ã„ãªã„ã®ã§ã€ãã®ç‚¹æ³¨æ„ã€‚

```powershell
PS C:\Users\yurio> docker pull mcr.microsoft.com/azure-cli:2.49.0
PS C:\> docker run -v c:/docker:/data -it mcr.microsoft.com/azure-cli:2.49.0
b17727d89c49:/#
```

* [Docker ã‚’ä½¿ç”¨ã™ã‚‹](https://learn.microsoft.com/ja-jp/azure/azure-resource-manager/templates/deployment-script-template-configure-dev#use-docker)

## èª²é‡‘

èª²é‡‘ã«éš›ã—ã¦ã¯è‡ªå‹•ç”Ÿæˆã•ã‚Œã‚‹ACIã¨ã‚¹ãƒˆãƒ¬ãƒ¼ã‚¸ ã‚¢ã‚«ã‚¦ãƒ³ãƒˆ (æ—¢å­˜ã®ã‚¹ãƒˆãƒ¬ãƒ¼ã‚¸ ã‚¢ã‚«ã‚¦ãƒ³ãƒˆã‚’ä½¿ç”¨ã™ã‚‹å ´åˆã€ä½œæˆã•ã‚Œã‚‹ãƒ•ã‚¡ã‚¤ãƒ«å…±æœ‰)ã«å¯¾ã™ã‚‹èª²é‡‘ãŒè¡Œã‚ã‚Œã‚‹ã€‚
æˆåŠŸæ™‚ã«ãã‚Œã‚‰ã‚’æ¶ˆã—ãŸã„å ´åˆã€```cleanupPreference```ã¯ã€```OnSuccess```ã¨æŒ‡å®šã—ã¦ãŠãã¨ã€ãƒˆãƒ©ãƒ–ãƒ«ã‚·ãƒ¥ãƒ¼ãƒ†ã‚£ãƒ³ã‚°æ™‚ã«å®Ÿè¡Œã•ã‚ŒãŸã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚„å‡ºåŠ›ãŒæ®‹ã‚‹ã®ã§ã“ã®æŒ‡å®šãŒè‰¯ã„ã‹ã‚‚ã€‚

```bicep
    cleanupPreference: 'OnSuccess'
    retentionInterval: 'P1D'
```

# å‚è€ƒæ–‡çŒ®

* [ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’ä½¿ç”¨ã—ã¦ Bicep ãŠã‚ˆã³ ARM ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆã‚’æ‹¡å¼µã™ã‚‹](https://learn.microsoft.com/ja-jp/training/modules/extend-resource-manager-template-deployment-scripts/)
* [Bicep ã§ãƒ‡ãƒ—ãƒ­ã‚¤ ã‚¹ã‚¯ãƒªãƒ—ãƒˆã‚’ä½¿ã†](https://learn.microsoft.com/ja-jp/azure/azure-resource-manager/bicep/deployment-script-bicep#clean-up-deployment-script-resources)
